LPAREN: "("
RPAREN: ")"
NOT: "!" | "~" | "\neg" | "¬"
AND: "&" | "\wedge" | "∧"
OR: "|" | "\vee" | "∨"
IMP: "=>" | "\implies" | "⇒" | "→" | "⟹"
IFF: "<=>" | "\iff" | "⇔" | "↔" | "⟺"
LITERAL: /[a-zA-Z]+/

?constant:  "true"                  -> on_true
          | "false"                 -> on_false
?literal:   LITERAL                 -> on_literal
?iffexpr:   impexpr
          | impexpr IFF iffexpr     -> on_iff
?impexpr:   orexpr
          | orexpr IMP impexpr      -> on_imp
?orexpr:    andexpr
          | andexpr OR orexpr       -> on_or
?andexpr:   notexpr
          | notexpr AND andexpr     -> on_and
?notexpr:   parenexpr
          | NOT parenexpr           -> on_not
?parenexpr: constant
          | literal
          | LPAREN iffexpr RPAREN   -> on_paren

?propexpr:  iffexpr                 -> on_result

%import common.WS
%ignore WS
